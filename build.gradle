plugins {
    id "io.spring.dependency-management" version "1.1.6" apply false
}

group "com.bettercloud"
version "1.0.6-custom"

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

allprojects {
    apply plugin: "io.spring.dependency-management"
    apply plugin: "maven-publish"
    apply plugin: "java-library"

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.3.5"
        }
    }
}

subprojects {
    apply plugin: "signing"

    group = rootProject.group
    archivesBaseName = project.name
    version = rootProject.version

    dependencies {
        implementation group: "com.google.guava", name: "guava", version: "$guava_version"
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        compileOnly group: "org.projectlombok", name: "lombok"
        annotationProcessor group: "org.projectlombok", name: "lombok"

        testImplementation "junit:junit"
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("publications") }
        sign configurations.archives
    }

    publishing {
        def nexusUser = hasProperty('NEXUS_CREDS_USR') ? nexusUsername : System.getenv('NEXUS_CREDS_USR')
        def nexusPassword = hasProperty('NEXUS_CREDS_PSW') ? nexusPassword : System.getenv('NEXUS_CREDS_PSW')
        def nexusUrl = hasProperty('NEXUS_URL') ? nexusUrl : System.getenv('NEXUS_URL')

        repositories {
            maven {
                url = nexusUrl
                credentials {
                    username = nexusUser
                    password = nexusPassword
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    name = 'SCIM 2.0 SDK for Spring'
                    packaging "jar"
                    description = 'SCIM 2.0 SDK for Spring'
                    url = 'https://github.com/BetterCloud/scim2'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/BetterCloud/scim2/blob/master/README.md'
                        }
                    }
                    developers {
                        developer {
                            id = 'daniel-lynch'
                            name = 'Daniel Lynch'
                            email = 'dantlynch@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

wrapper {
    gradleVersion = '7.5'
}